plugins {
    id 'idea'
    id 'maven-publish'
    id 'java-library'
    id 'net.neoforged.moddev'
}

base {
    archivesName = 'peekaboo-api'
}

def env = System.getenv()
if (env.RELEASE) {
    version = mod_version
} else {
    version = "$mod_version+local"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = parchment_version
    }
}

java {
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "FMLModType"              : "GAMELIBRARY",
                "Specification-Title"     : "peekaboo-api",
                "Specification-Vendor"    : "lovetropics",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "lovetropics",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing.publications.register('mavenJava', MavenPublication) {
    artifactId = 'peekaboo-api'
    from components.java
}
